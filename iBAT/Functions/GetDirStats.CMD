::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:GetDirStats %dir% files dirs bytes
::
:: 作者：Ritchie Lawrence, updated 2003-09-18. 版本 1.1
::
:: 功能：获取指定目录中的文件个数、子文件夹个数以及目录大小（包括 (includes
::       子文件夹、隐藏/系统文件及目录）。适用于 NT4/2000/XP/2003
::
:: 参数：%1 路径名称，绝对路径或者相对路径（传值调用）
::       %2 该变量用于接受文件个数（引用调用）
::       %3 该变量用于接受子文件夹个数（引用调用）
::       %4 该变量用于接受目录大小（引用调用）
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
setlocal ENABLEEXTENSIONS & set "var="
pushd %1 2>nul || (md;2>nul & endlocal & goto :EOF)
for /f "tokens=1-5* delims=," %%a in ('compact/s /q^|findstr ^^^^[0-O]') ^
do (call set "var=%%var%% %%a%%b%%c%%d%%e")
for /f "tokens=2,5,15" %%a in ('echo/%var%') do (popd & endlocal ^
  & set "%4=%%c" & set/a %3=%%b-1 & set/a "%2=%%a-%%b+1" & goto :EOF)
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::  