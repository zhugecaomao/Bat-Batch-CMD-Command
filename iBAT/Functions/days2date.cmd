:::::::::::::::::::::::::::::天数转日期函数::::::::::::::::::::::::::::
:days2date
:: 用法1：计算从 1970 年 1 月 1 日到给定的日期之间所经过的天数。
::  call :DaysToDate %days% yy mm dd
::  参数：%1 在公元纪年中所经过的天数
::        %2 该变量用于接收年（可选）
::        %3 该变量用于接收月（可选）
::        %4 该变量用于接收日（可选）
::
:: 用法2：日期偏移计算
::  call :DaysToDate %yy% %mm% %dd% %days% yy mm dd
::  参数：%1 该变量用于接收起始年
::        %2 该变量用于接收起始月
::        %3 该变量用于接收起始日
::        %4 偏移天数
::        %5 该变量用于接收年（可选）
::        %6 该变量用于接收月（可选）
::        %7 该变量用于接收日（可选）
::
:: 注：可选项若为空，则显示结果到屏幕，否则把结果存储到相应变量中
::
::                                Made by Crlf     http://bbs.bathome.net
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
setlocal disabledelayedexpansion&set d=%1&set /a "1/(!%20+!%30+!%40)" 2>nul||(
set/a "d=10%~3%%100-719162+30*(m=10%~2%%100)+m/9*-~m/2+!(m/9)*m/2+!!(m/3)*(!((y=%1)%%4)-!(y%%100)+!(y%%400)-2)+y*365+~-y/4-~-y/100+~-y/400-396+%4"
shift&shift&shift)
set/a d-=(d+=719162)/1460-d/36524+d/146096,y=d/365+1,r=!(y%%4)-!(y%%100)+!(y%%400),d+=(d%%=365)/(212+r)*30+r,d+=!!(d/(59+r))*(2-r),m=d/61*2+d%%61/31,d-=m*61/2+m*61%%2-101,m+=101-m/8
endlocal&2>nul set %4=%d:~-2%&&set %3=%m:~-2%&&set %2=%y%||echo %y% %m% %d%
if /i %0==:days2date exit/b0